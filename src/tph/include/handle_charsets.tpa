DEFINE_ACTION_FUNCTION fl#HANDLE_CHARSETS#WHICH#INFER
  STR_VAR
    language = ~~
  RET
    charset
BEGIN
  ACTION_MATCH "%language%" WITH
    ".*schinese.*" ".*zh_CN.*"
    BEGIN
      OUTER_SPRINT charset "CP936"
    END

    /* Doubts remain about whether CP950 is the correct charset for
     * Traditional Chinese, mainly due to the translation of the
     * BG2 Fixpack, which does not appear to be perfectly represented
     * by CP950.
     * Possible other alternatives include something involving the
     * Hong Kong Supplementary Character Set (HKSCS) or Windows'
     * non-standard CP950+CP951 hack (which appears to be unavailable
     * outside of Traditionally Chinese Windows).
     * Cf. Wikipedia on Big5.
     */
    ".*tchinese.*"
    BEGIN
      OUTER_SPRINT charset "CP950"
    END

    ".*czech.*" ".*cs_CZ.*"
    BEGIN
      OUTER_SPRINT charset "CP1250"
    END

    ".*english.*" ".*american.*" ".*en_US.*"
    BEGIN
      OUTER_SPRINT charset "CP1252"
    END

    ".*french.*" ".*francais.*" ".*fr_FR.*"
    BEGIN
      OUTER_SPRINT charset "CP1252"
    END

    ".*german.*" ".*deutsch.*" ".*de_DE.*"
    BEGIN
      OUTER_SPRINT charset "CP1252"
    END

    ".*italian.*" ".*italiano.*" ".*it_IT.*"
    BEGIN
      OUTER_SPRINT charset "CP1252"
    END

    ".*japanese.*" ".*ja_JP.*"
    BEGIN
      OUTER_SPRINT charset "CP932"
    END

    ".*korean.*" ".*ko_KR.*"
    BEGIN
      OUTER_SPRINT charset "CP949"
    END

    ".*polish.*" ".*polski.*" ".*pl_PL.*"
    BEGIN
      OUTER_SPRINT charset "CP1250"
    END

    ".*russian.*" ".*ru_RU.*"
    BEGIN
      OUTER_SPRINT charset "CP1251"
    END

    ".*spanish.*" ".*castilian.*" ".*espanol.*" ".*castellano.*" ".*es_ES.*"
    BEGIN
      OUTER_SPRINT charset "CP1252"
    END

    DEFAULT
      FAIL "ERROR: charset could not be inferred for language %language%"
  END
END

DEFINE_ACTION_FUNCTION fl#HANDLE_CHARSETS#WHICH
  INT_VAR
    infer_charsets = 0
  STR_VAR
    language = ~~
    charset_table = ~~
  RET
    charset
BEGIN
  ACTION_IF infer_charsets BEGIN
    LAF fl#HANDLE_CHARSETS#WHICH#INFER STR_VAR language RET charset END
  END ELSE BEGIN
    ACTION_TO_LOWER language
    ACTION_IF VARIABLE_IS_SET $EVAL "%charset_table%"("%language%") BEGIN
      OUTER_SPRINT charset $EVAL "%charset_table%"("%language%")
    END ELSE BEGIN
      FAIL "ERROR: no tabulated charset could be found for language %language%"
    END
  END
END

DEFINE_ACTION_FUNCTION fl#HANDLE_CHARSETS#CONVERT
  STR_VAR
    iconv = ~iconv~
    quote = ~"~
    in_path = ~~
    out_path = ~~
    file = ~~
    from_charset = ~123fakecharset~
    to_charset = ~UTF-8~
BEGIN
  /*
   * iconv will happily be told to use an incorrect -f encoding and
   * produce invalid output, so may as well provide the -c option.
   */
  OUTER_SPRINT c_option "-c"
  OUTER_SPRINT infile ~%in_path%/%file%~
  ACTION_IF ~%in_path%~ STR_EQ ~%out_path%~ BEGIN
    OUTER_SPRINT outfile ~%in_path%/fl#utf8_%file%~
  END ELSE OUTER_SPRINT outfile ~%out_path%/%file%~
  ACTION_TRY
    COPY "%infile%" "%outfile%" // so outfile is uninstalled
    /*
     * The -o option does not appear to be supported by all implementations
     */
    AT_NOW ~%iconv% %c_option% -f %from_charset% -t %to_charset% %quote%%infile%%quote% > %quote%%outfile%%quote%~
    ACTION_IF ~%in_path%~ STR_EQ ~%out_path%~ BEGIN
      OUTER_SPRINT dest ~%infile%~
    END ELSE OUTER_SPRINT dest ~%outfile%~
    COPY "%outfile%" "%dest%"
      REPLACE_TEXTUALLY
        ~-\*-[%TAB% ]*\(en\)?coding:[%TAB% ]*%from_charset%[%TAB% ]*-\*-~
        ~-*- coding: %to_charset% -*-~
  WITH
    DEFAULT
      PRINT "ERROR: unable to convert %infile% from %from_charset% into %to_charset%"
      ACTION_RERAISE
  END
END

DEFINE_ACTION_FUNCTION fl#HANDLE_CHARSETS#RECURSE
  STR_VAR
    in_path = ""
    out_path = ""
    type = ""
    function = ""
BEGIN
  ACTION_IF verbose BEGIN
    PRINT "#RECURSE is calling terminal function on %in_path% with out_path: %out_path%"
  END
  ACTION_BASH_FOR "%in_path%" ".+\.%type%$" BEGIN
    LAF "%function%"
      STR_VAR
        file = EVAL "%BASH_FOR_FILE%"
        in_path = EVAL "%BASH_FOR_DIRECTORY%"
        out_path
    END
  END
  ACTION_CLEAR_ARRAY dir_array
  GET_DIRECTORY_ARRAY dir_array "%in_path%" ".+"
  ACTION_PHP_EACH EVAL dir_array AS _ => in_path BEGIN
    ACTION_IF "%in_path%" STRING_MATCHES_REGEXP ".*\.$" BEGIN
      LAF RES_OF_FILESPEC
        STR_VAR
          filespec = EVAL "%in_path%"
        RET
          directory = res
      END
      ACTION_IF verbose BEGIN
        PRINT "#RECURSE is recursing through %in_path% with out_path: %out_path%/%directory%"
      END
      LAF fl#HANDLE_CHARSETS#RECURSE
        STR_VAR
          in_path
          out_path = EVAL "%out_path%/%directory%"
          type
          function
      END
    END
  END
END

DEFINE_ACTION_FUNCTION fl#HANDLE_CHARSETS#RECURSE#TERMINAL
  STR_VAR
    file = ""
    in_path = ""
    out_path = ""
BEGIN
  OUTER_SET convert = 1
  /* This makes for a more consistent interface, albeit slightly
   * messier code. noconvert_array is very likely to be short.
   */
  ACTION_PHP_EACH "%noconvert_array%" AS _ => noconvert_file BEGIN
    ACTION_IF "%noconvert_file%" STRING_MATCHES_REGEXP ".+\.tra$" = 0 BEGIN
      OUTER_SPRINT match "%noconvert_file%"
    END ELSE OUTER_SPRINT match "%noconvert_file%.tra"
    ACTION_IF "%match%" STRING_EQUAL_CASE "%file%" BEGIN
      OUTER_SET convert = 0
    END
  END
  ACTION_IF convert BEGIN
    ACTION_IF verbose BEGIN
      PRINT "Converting %in_path%/%file% to %out_path%/%file%"
    END
    LAF fl#HANDLE_CHARSETS#CONVERT
      STR_VAR
        iconv // from the calling environment (curse the lack of closures)
        quote // from the calling environment
        in_path
        out_path
        file
        from_charset // from the calling environment
        to_charset // from the calling environment
    END
  END
END

DEFINE_ACTION_FUNCTION HANDLE_CHARSETS
  INT_VAR
    infer_charsets = 0
    verbose = 0
    from_utf8 = 0
  STR_VAR
    tra_path = ~~
    out_path = EVAL ~%tra_path%~
    default_language = ~~
    language = EVAL ~%LANGUAGE%~
    iconv_path = EVAL ~%tra_path%/iconv~
    charset_table = ~~
    convert_array = ~~
    noconvert_array = ~~
    reload_array = ~~
BEGIN
  /* Early versions of BG: EE do not include bgee.lua and
   * PST: EE does not include monkfist.2da
   */
  ACTION_IF ((FILE_EXISTS_IN_GAME bgee.lua OR
            FILE_EXISTS_IN_GAME monkfist.2da) AND
            !from_utf8) OR
            (!FILE_EXISTS_IN_GAME bgee.lua AND
            !FILE_EXISTS_IN_GAME monkfist.2da AND
            from_utf8)
  BEGIN
    ACTION_IF verbose BEGIN
      PRINT "tra_path: %tra_path%"
      PRINT "out_path: %out_path%"
    END
    /*
     * Initial versions mistakenly called the variable infer_charset.
     * For reasons of backwards-compatibility, we use infer_charset
     * to initialise infer_charsets, if appropriate.
     */
    OUTER_SET infer_charsets = (IS_AN_INT infer_charset AND infer_charset) AND !infer_charsets ? 1 : infer_charsets
    ACTION_MATCH ~%WEIDU_OS%~ WITH
      win32
      BEGIN
        OUTER_SPRINT iconv ~%iconv_path%/iconv.exe~
        OUTER_SPRINT quote ~"~
      END

      osx unix
      BEGIN
        OUTER_SPRINT iconv ~iconv~
        OUTER_SPRINT quote ~'~
      END

      DEFAULT
    END
    ACTION_IF ("%WEIDU_OS%" STRING_EQUAL_CASE "win32" AND FILE_EXISTS "%iconv%") OR
              ("%WEIDU_OS%" STRING_EQUAL_CASE "osx" OR "%WEIDU_OS%" STRING_EQUAL_CASE "unix")
    BEGIN
      ACTION_IF "%default_language%" STR_EQ "%language%" BEGIN
        OUTER_SPRINT default_language "" // do not process twice
      END
      ACTION_FOR_EACH language IN "%default_language%" "%language%" BEGIN
        ACTION_IF "%language%" STR_CMP "" AND !FILE_EXISTS ~%tra_path%/%language%/fl#utf8.mrk~ BEGIN
          LAF fl#HANDLE_CHARSETS#WHICH
            INT_VAR
              infer_charsets
            STR_VAR
              language
              charset_table
            RET
              charset
          END
          ACTION_IF !from_utf8 BEGIN
            OUTER_SPRINT from_charset "%charset%"
            OUTER_SPRINT to_charset "UTF-8"
          END ELSE BEGIN
            OUTER_SPRINT from_charset "UTF-8"
            OUTER_SPRINT to_charset "%charset%"
          END
          ACTION_IF verbose BEGIN
            PRINT "Conversion is from unicode?: %from_utf8%"
            PRINT "from_charset identified as %from_charset%"
            PRINT "to_charset identifed as %to_charset%"
          END
          ACTION_IF VARIABLE_IS_SET $EVAL "%convert_array%"(0) BEGIN
            ACTION_PHP_EACH "%convert_array%" AS _ => filename BEGIN
              ACTION_IF "%filename%" STRING_MATCHES_REGEXP ".+\.tra$" = 0 BEGIN
                OUTER_SPRINT file "%filename%"
              END ELSE OUTER_SPRINT file "%filename%.tra"
              ACTION_IF verbose BEGIN
                PRINT "convert_array: %file%"
              END
              LAF fl#HANDLE_CHARSETS#CONVERT
                STR_VAR
                  iconv
                  quote
                  in_path = EVAL ~%tra_path%/%language%~
                  out_path = EVAL ~%out_path%/%language%~
                  file
                  from_charset
                  to_charset
              END
            END
          END ELSE BEGIN
            ACTION_IF verbose BEGIN
              PRINT "Recursing through %tra_path%/%language%"
            END
            LAF fl#HANDLE_CHARSETS#RECURSE
              STR_VAR
                in_path = EVAL ~%tra_path%/%language%~
                out_path = EVAL ~%out_path%/%language%~
                type = ~tra~
                function = ~fl#HANDLE_CHARSETS#RECURSE#TERMINAL~
            END
          END
          ACTION_PHP_EACH "%reload_array%" AS _ => file BEGIN
            ACTION_IF verbose BEGIN
              PRINT "reload_array: %file%"
            END
            ACTION_IF "%file%" STRING_MATCHES_REGEXP ".+\.tra$" = 0 BEGIN
              LOAD_TRA ~%out_path%/%language%/%file%~
            END ELSE LOAD_TRA ~%out_path%/%language%/%file%.tra~
          END
          ACTION_IF ~%tra_path%~ STR_EQ ~%out_path%~ BEGIN
            COPY_EXISTING bolt01.itm ~%tra_path%/%language%/fl#utf8.mrk~
          END
        END
      END
    END ELSE BEGIN
      FAIL "ERROR: charsets were not converted because iconv could not be found in %iconv_path%"
    END
  END
END
